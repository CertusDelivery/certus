app.controller('PicklistCtrl', ['$scope', '$resource', '$http', '$modal', ($scope, $resource, $http, $modal) ->
  picklist = $resource('/api/deliveries/picklist.json')
  $scope.picklist = picklist.query()
  $scope.location_sort = 'asc'
  $scope.mySelections = []
  cellEditableLocation = "<input ng-class=\"'colt' + col.index\" ng-input=\"COL_FIELD\" ng-model=\"COL_FIELD\"  ng-blur=\"updateEntity(row.entity)\" />"
  cellLocation = '<div ng-click=\"removeFocus()\">{{row.getProperty(col.field)}}</div>'
  cellTemplatePicked = '<div ng-class="{hidden: row.getProperty(col.field) == \'PICKED\' ? false : true }" class="picked">âœ“</div>'

  $scope.gridOptions = {
    data: 'picklist',
    columnDefs: [
      {field: 'picking_progress', displayName: 'Qty', width: '10%'},
      {field: 'product_name', displayName: 'Product', width: '40%'},
      {field: 'shipping_weight', visible: false},
      {field: 'location', displayName: 'Location', sortable: false, cellTemplate: cellLocation, enableCellEdit: true, enableCellEditOnFocus:true, editableCellTemplate: cellEditableLocation, width: '15%'},
      {field: 'delivery_id', displayName: 'ID', width: '10%'},
      {field: 'picked_status', displayName: 'Picked', width: '10%', cellTemplate: cellTemplatePicked},
      {field: 'store_sku', displayName: 'SKU', width: '15%'},
      {field: 'id', visible: false}
    ],
    selectedItems: $scope.mySelections,
    multiSelect: false,
    enableHighlighting: true,
    enableCellEditOnFocus: true,
    sortInfo: { fields: ['product_name'], directions: ['asc']}
  }

  $scope.$on('ngGridEventData', ->
    $scope.selectUnpickedRow()
  )

  $scope.selectUnpickedRow = ->
    found = false
    angular.forEach $scope.gridOptions.$gridScope.renderedRows, (row, index) ->
      return if found
      if row.entity.picked_status == 'UNPICKED'
        $scope.gridOptions.selectRow(index, true)
        found = true

  $scope.clearSortingData = ->
    if ($scope.gridOptions)
      $scope.gridOptions.ngGrid.config.sortInfo = { fields:[], directions: [] }
      angular.forEach($scope.gridOptions.ngGrid.lastSortedColumns, (c) ->
        c.sortPriority = null
        c.sortDirection = ""
      )
      $scope.gridOptions.ngGrid.lastSortedColumns = []

  $scope.refreshUnpickedCount = ->
    $http.get('/api/deliveries/unpicked_orders').success( (data) ->
      $scope.unpicked_count = data
    )

  $scope.loadNewOrder = ->
    $scope.notice = ''
    $scope.errorMessage = ''
    loader = $resource('/api/deliveries/load_unpicked_order.json')
    $scope.picklist = loader.query()
    $scope.refreshUnpickedCount()

  $scope.loadOrdersByLocation = ->
    $scope.clearSortingData()
    loader = $resource('/api/deliveries/sort_picking_orders.json', {direction: $scope.location_sort})
    if $scope.location_sort == 'asc'
      $scope.location_sort = 'desc'
    else
      $scope.location_sort = 'asc'
    $scope.picklist = loader.query()


  $scope.addFocus = ->
    $scope.focusScanner = true

  $scope.removeFocus = ->
    $scope.focusScanner = false

  $scope.timer = ->
    today = new Date()
    $scope.time = today.toTimeString()

  $scope.focusScanner = true
  $scope.refreshUnpickedCount()
  $scope.timer()
  setInterval(
    ->
      $('#current_time').click()
    1000
  )
  setInterval(
    ->
      $('#unpicked_order_count').click()
    5000
  )

  $scope.picking = (barcode) ->
    return false unless barcode
    deliveryItem = $scope.mySelections[0]
    $http.post('/api/delivery_items/pick.json',
       barcode: barcode,
       id: deliveryItem.id,
       delivery_id: deliveryItem.delivery_id
    ).success((data) ->
      $scope.scannedBarcode = ''
      $scope.notice = data.message if data.message
      if data.remove_completed_delivery
        $scope.picklist = picklist.query()
      else
        angular.forEach $scope.picklist, (row, index) ->
          if row.id == data.id
            angular.forEach data, (v, k) ->
              row[k] = v
            $scope.gridOptions.selectItem(index, true)
            $('.ngGrid').find('.selected').fadeTo('fast',0).fadeTo('fast',1)
            if data.picked_status is 'PICKED'
              $scope.selectUnpickedRow()
    ).error((data) ->
      if data.status is 'nok'
        $scope.scannedBarcode = ''
        $scope.errorMessage = data.message
    )

  # operation_code: 1 -> normal, 2 -> out of stock
  $scope.pickProduct = (operation_code) ->
    $scope.notice = ''
    $scope.errorMessage = ''
    barcode = $scope.scannedBarcode
    deliveryItem = $scope.mySelections[0]
    if operation_code == 2
      barcode = "OUT_OF_STOCK"
    else if barcode isnt "REMOVE_COMPLETED_DELIVERY" and barcode isnt "OUT_OF_STOCK"
      if !!barcode and deliveryItem.out_of_stock_quantity isnt 0 and deliveryItem.is_replaced is false and deliveryItem.store_sku isnt barcode
        $scope.substituteProduct(deliveryItem, barcode)
        return false
    $scope.picking(barcode)

  $scope.markAsOutOfStock = ->
    $scope.pickProduct(2)

  $scope.substituteProduct = (deliveryItem, substituteBarcode) ->
    $http.get('api/products/search/'+substituteBarcode+'.json'
    ).success((product) ->
      ensureMsg = 'Do you want to substitude ' + product.product_name + ' for ' + deliveryItem.product_name + '?'
      if confirm ensureMsg
        $http.post('api/delivery_items/'+deliveryItem.id+'/substitute.json',
          product: product
        ).success((data) ->
          deliveryItem.is_replaced = true
          $scope.scannedBarcode = ''
          needInsertFlag = true
          angular.forEach $scope.picklist, (row, index) ->
            if row.id == data.id
              needInsertFlag = false
              angular.forEach data, (v, k) ->
                row[k] = v
              $scope.gridOptions.selectItem(index, true)
          if needInsertFlag
            index = $scope.picklist.push data
            setTimeout( ->
              $scope.gridOptions.selectItem(index-1, true)
            , 0)
        )
      else
        $scope.picking(substituteBarcode)
    ).error((data) ->
      $scope.scannedBarcode = ''
      $scope.errorMessage = data.message
    )


  $scope.removePickedOrders = ->
    $scope.notice = ''
    $scope.errorMessage = ''
    $http.delete('/api/deliveries/remove_picked_orders.json'
    ).success((data) ->
      $scope.notice = data.message if data.message
      $scope.picklist = picklist.query()
    )

  $scope.updateEntity = (row) ->
    if row.location is ''
      picklist = $resource('/api/deliveries/picklist.json')
      $scope.picklist = picklist.query()
    else
      $http.post('/api/delivery_items/'+row.id+'/update_location',
        location: row.location
      ).success((data) ->
        if data.status is false
          row.location = data.location
      )
    $scope.addFocus()

  $scope.productDetail= (row) ->
    $scope.selectedProduct = $scope.mySelections[0]
    $scope.selectedProduct.modal = $modal.open {
      templateUrl: "<%= asset_path('angular/templates/product_detail.html') %>",
      scope: ( ->
        $scope
      )()
   }
])
